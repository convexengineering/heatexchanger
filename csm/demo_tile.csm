# demo_tile
# written by John Dannenhoffer

# default parameters (may be overwritten by udfTile)
despmtr   thkPlate:000    0.10
despmtr   thkPlate:001    0.10
despmtr   thkPlate:010    0.10
despmtr   thkPlate:011    0.10
despmtr   thkPlate:100    0.10
despmtr   thkPlate:101    0.10
despmtr   thkPlate:110    0.10
despmtr   thkPlate:111    0.10

despmtr   thkHot:000      0.04
despmtr   thkHot:001      0.04
despmtr   thkHot:010      0.04
despmtr   thkHot:011      0.04
despmtr   thkHot:100      0.04
despmtr   thkHot:101      0.04
despmtr   thkHot:110      0.04
despmtr   thkHot:111      0.04

despmtr   thkCold:000     0.04
despmtr   thkCold:001     0.04
despmtr   thkCold:010     0.04
despmtr   thkCold:011     0.04
despmtr   thkCold:100     0.04
despmtr   thkCold:101     0.04
despmtr   thkCold:110     0.04
despmtr   thkCold:111     0.04

despmtr   hot2cold:000    0.30
despmtr   hot2cold:001    0.30
despmtr   hot2cold:010    0.30
despmtr   hot2cold:011    0.30
despmtr   hot2cold:100    0.30
despmtr   hot2cold:101    0.30
despmtr   hot2cold:110    0.30
despmtr   hot2cold:111    0.30

# average conditions (for y-extents of hot and cold plates)
set       thkPlate       "(thkPlate:000+thkPlate:001 \
                          +thkPlate:010+thkPlate:011 \
                          +thkPlate:100+thkPlate:101 \
                          +thkPlate:110+thkPlate:111)/8"
set       hot2cold       "(hot2cold:000+hot2cold:001 \
                          +hot2cold:010+hot2cold:011 \
                          +hot2cold:100+hot2cold:101 \
                          +hot2cold:110+hot2cold:111)/8"
set       yCold          "(1-2*thkPlate)/(1+hot2cold)"
set       yHot           "yCold*hot2cold"
set       ylo            "(yHot+thkPlate)/2"
set       yhi            "1-ylo"

# upper hot fin (centered at z=0.50)
set       thkx00    (thkHot:000+thkHot:100)/2
set       thkx01    (thkHot:001+thkHot:101)/2
set       thkx10    (thkHot:010+thkHot:110)/2
set       thkx11    (thkHot:011+thkHot:111)/2

udprim    hex   corners  "0.0; yhi; 0.5-thkx00/2; \
                          1.0; yhi; 0.5-thkx01/2; \
                          0.0; 1.0; 0.5-thkx10/2; \
                          1.0; 1.0; 0.5-thkx11/2; \
                          0.0; yhi; 0.5+thkx00/2; \
                          1.0; yhi; 0.5+thkx01/2; \
                          0.0; 1.0; 0.5+thkx10/2; \
                          1.0; 1.0; 0.5+thkx11/2"
   select    face   @nbody 5
   attribute _color $red
   select    face   @nbody 6
   attribute _color $red
 
# upper plate (centered at y=yhi)
set       h2c       (hot2cold:000+hot2cold:010)/2
set       thk0x0    (thkPlate:000+thkPlate:010)/2
set       y0x0      1-((1-2*thk0x0)/(1+h2c)*h2c+thk0x0)/2

set       h2c       (hot2cold:001+hot2cold:011)/2
set       thk0x1    (thkPlate:001+thkPlate:011)/2
set       y0x1      1-((1-2*thk0x1)/(1+h2c)*h2c+thk0x1)/2

set       h2c       (hot2cold:100+hot2cold:110)/2
set       thk1x0    (thkPlate:100+thkPlate:110)/2
set       y1x0      1-((1-2*thk1x0)/(1+h2c)*h2c+thk1x0)/2

set       h2c       (hot2cold:101+hot2cold:111)/2
set       thk1x1    (thkPlate:101+thkPlate:111)/2
set       y1x1      1-((1-2*thk1x1)/(1+h2c)*h2c+thk1x1)/2

udprim    hex   corners  "0.0; y0x0-thk0x0/2; 0.0; \
                          1.0; y0x1-thk0x1/2; 0.0; \
                          0.0; y0x0+thk0x0/2; 0.0; \
                          1.0; y0x1+thk0x1/2; 0.0; \
                          0.0; y1x0-thk1x0/2; 1.0; \
                          1.0; y1x1-thk1x1/2; 1.0; \
                          0.0; y1x0+thk1x0/2; 1.0; \
                          1.0; y1x1+thk1x1/2; 1.0"
   select    face   @nbody 4
   attribute _color $red
   select    face   @nbody 2
   attribute _color $blue
union

# middle cold fin (centered at x=0.50)
set       thk00x    (thkCold:000+thkCold:001)/2 
set       thk01x    (thkCold:010+thkCold:011)/2
set       thk10x    (thkCold:100+thkCold:101)/2
set       thk11x    (thkCold:110+thkCold:111)/2

udprim    hex   corners  "0.50-thk00x/2; ylo; 0.0; \
                          0.50+thk00x/2; ylo; 0.0; \
                          0.50-thk01x/2; yhi; 0.0; \
                          0.50+thk01x/2; yhi; 0.0; \
                          0.50-thk10x/2; ylo; 1.0; \
                          0.50+thk10x/2; ylo; 1.0; \
                          0.50-thk11x/2; yhi; 1.0; \
                          0.50+thk11x/2; yhi; 1.0"
   select    face   @nbody 1
   attribute _color $blue
   select    face   @nbody 3
   attribute _color $blue
union

# lower plate (centered at y=ylo)
set       h2c       (hot2cold:000+hot2cold:010)/2
set       thk0x0    (thkPlate:000+thkPlate:010)/2
set       y0x0      ((1-2*thk0x0)/(1+h2c)*h2c+thk0x0)/2

set       h2c       (hot2cold:001+hot2cold:011)/2
set       thk0x1    (thkPlate:001+thkPlate:011)/2
set       y0x1      ((1-2*thk0x1)/(1+h2c)*h2c+thk0x1)/2

set       h2c       (hot2cold:100+hot2cold:110)/2
set       thk1x0    (thkPlate:100+thkPlate:110)/2
set       y1x0      ((1-2*thk1x0)/(1+h2c)*h2c+thk1x0)/2

set       h2c       (hot2cold:101+hot2cold:111)/2
set       thk1x1    (thkPlate:101+thkPlate:111)/2
set       y1x1      ((1-2*thk1x1)/(1+h2c)*h2c+thk1x1)/2

udprim    hex   corners  "0.0; y0x0-thk0x0/2; 0.0; \
                          1.0; y0x1-thk0x1/2; 0.0; \
                          0.0; y0x0+thk0x0/2; 0.0; \
                          1.0; y0x1+thk0x1/2; 0.0; \
                          0.0; y1x0-thk1x0/2; 1.0; \
                          1.0; y1x1-thk1x1/2; 1.0; \
                          0.0; y1x0+thk1x0/2; 1.0; \
                          1.0; y1x1+thk1x1/2; 1.0"
   select    face   @nbody 2
   attribute _color $red
   select    face   @nbody 4
   attribute _color $blue
union

# lower hot fin (centered at z=0.50)
set       thkx00    (thkHot:000+thkHot:100)/2
set       thkx01    (thkHot:001+thkHot:101)/2
set       thkx10    (thkHot:010+thkHot:110)/2
set       thkx11    (thkHot:001+thkHot:111)/2

udprim    hex   corners  "0.0; 0.0; 0.50-thkx00/2; \
                          1.0; 0.0; 0.50-thkx01/2; \
                          0.0; ylo; 0.50-thkx10/2; \
                          1.0; ylo; 0.50-thkx11/2; \
                          0.0; 0.0; 0.50+thkx00/2; \
                          1.0; 0.0; 0.50+thkx01/2; \
                          0.0; ylo; 0.50+thkx10/2; \
                          1.0; ylo; 0.50+thkx11/2"
   select    face   @nbody 5
   attribute _color $red
   select    face   @nbody 6
   attribute _color $red
union

#======================================#
# automatically generated verification #
# OpenCSM  1.13      OpenCASCADE 6.8.1 #
#======================================#
select    body 9
   assert         3      @itype       0  1
   assert        56      @nnode       0  1
   assert        92      @nedge       0  1
   assert        38      @nface       0  1
   assert   2.32000e-01  @volume  -.001  1
   assert   6.38400e+00  @area    -.001  1
   assert   5.00000e-01  @xcg     -.001  1
   assert   5.00000e-01  @ycg     -.001  1
   assert   5.00000e-01  @zcg     -.001  1

end
